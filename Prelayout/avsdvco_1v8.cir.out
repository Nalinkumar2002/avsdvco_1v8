
* ==================================== *
* IP Name :avsdvco_1V8
* PDK : Sky130 PDK by Google SkyWater
* ==================================== *

* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*  Name of the author : NALINKUMAR S 
* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

* =================================== *
 .TITLE avsd_vco_1V8 analysis @ 0.9 V 
* =================================== *


* === To INCLUDE skywater-130nm model library === *
.lib "sky130_fd_pr/models/sky130.lib.spice" tt


* === Skywater 130nm CMOS Models === *

xM1 9 8 3 3 sky130_fd_pr__pfet_01v8 l=0.18 w=0.42
xM2 10 9 4 4 sky130_fd_pr__pfet_01v8 l=0.18 w=0.42
xM3 11 10 5 5 sky130_fd_pr__pfet_01v8 l=0.18 w=0.42
xM4 12 11 6 6 sky130_fd_pr__pfet_01v8 l=0.18 w=0.42
xM5 8 12 7 7 sky130_fd_pr__pfet_01v8 l=0.18 w=0.42

xM6 9 8 14 14 sky130_fd_pr__nfet_01v8 l=0.18 w=0.42
xM7 10 9 15 15 sky130_fd_pr__nfet_01v8 l=0.18 w=0.42
xM8 11 10 16 16 sky130_fd_pr__nfet_01v8 l=0.18 w=0.42
xM9 12 11 17 17 sky130_fd_pr__nfet_01v8 l=0.18 w=0.42
xM10 8 12 18 18 sky130_fd_pr__nfet_01v8 l=0.18 w=0.42

xM11 3 2 1 1 sky130_fd_pr__pfet_01v8 l=0.18 w=1.08
xM12 4 2 1 1 sky130_fd_pr__pfet_01v8 l=0.18 w=1.08
xM13 5 2 1 1 sky130_fd_pr__pfet_01v8 l=0.18 w=1.08
xM14 6 2 1 1 sky130_fd_pr__pfet_01v8 l=0.18 w=1.08
xM15 7 2 1 1 sky130_fd_pr__pfet_01v8 l=0.18 w=1.08

xM16 14 19 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=1.08
xM17 15 19 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=1.08
xM18 16 19 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=1.08
xM19 17 19 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=1.08
xM20 18 19 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=1.08

xM25 2 2 bias 1 sky130_fd_pr__pfet_01v8 l=0.18 w=0.84
xM26 2 19 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=0.84



xM21 13 8 1 1 sky130_fd_pr__pfet_01v8 l=0.18 w=2.4
xM23 13 8 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=1.2
xM22 F_out 13 1 1 sky130_fd_pr__pfet_01v8 l=0.18 w=1.08
xM24 F_out 13 0 0 sky130_fd_pr__nfet_01v8 l=0.18 w=0.54

V_Ibias 1 bias 0

* === DC VOLTAGE SOURCE (V=1.8V) === *
VDD 1 0 DC=1.8

* === DC CONTROL VOLTAGE (V=0.9V) === *
* NOTE: VCTRL can be varied from 0.4 V to 1.6 V to get various output frequency 

Vctrl 19 0 DC=0.9

* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ *
* === COMMANDS FOR SIMULATION === * 
* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ *

.control

* --- Transient analysis --- *
tran 1ps 10ns

* --- Changing Plot styles --- *
set color2 = orange
set xbrushwidth = 2.5

* --- Plotting waveform --- *
plot v(F_out)

print allv > plot_data_v.txt
print alli > plot_data_i.txt


.endc
.end
